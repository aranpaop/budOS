;主引导程序
;-----------------------------------
SECTION MBR vstart=0x7c00
    mov ax, cs
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov fs, ax
    mov sp, 0x7c00

;0x06号功能上卷全部行清屏
;-----------------------------------
;INT 0x10 功能号：0x06 功能描述：上卷窗口
;-----------------------------------
;输入：
;AH = 0x06 功能号
;AL = 上卷的行数（如果为0，表示全部）
;BH = 上卷行属性
;(CL, CH) = 窗口左上角的（X，Y）位置
;(DL, DH) = 窗口右下角的（X，Y）位置
;无返回值
;-----------------------------------
    mov ax, 0x600
    mov bx, 0x700
    mov cx, 0 ;左上角（0，0）
    mov dx, 0x184f ;右下角（25，80）
    int 0x10

;0x03号功能获取光标位置
;-----------------------------------
;INT 0x10 功能号：0x03 功能描述：获取光标位置
;-----------------------------------
;输入：
;AH = 0x03 功能号
;BH = 待获取光标的页号
;输出：
;CH = 光标开始行
;CL = 光标结束行
;DH = 光标所在行号
;DL = 光标所在列号
;-----------------------------------
    mov ah, 3
    mov bh, 0
    int 0x10

;0x13号功能打印字符串
;-----------------------------------
;INT 0x10 功能号：0x13 功能描述：打印字符串
;-----------------------------------
;输入：
;AH = 0x13 功能号
;ES:BP = 串首地址
;DX = 光标位置（CX中的可忽略）
;CX = 串长度（不包括结束符0）
;AL = 设置写字符方式，0x00：显示字符串，光标返回起始位置
;                     0x01：显示字符串，光标跟随移动
;                     0x02：显示字符串及其属性，光标返回起始位置
;                     0x03：显示字符串及其属性，光标跟随移动
;BH = 显示页号
;BL = 字符属性，0x02：黑底绿字
;-----------------------------------
    mov ax, message
    mov bp, ax ;es已初始化与cs一致
    mov cx, 21
    mov ax, 0x1301
    mov bx, 0x0200
    int 0x10

    jmp $

    message db "Buddingpop is coming~"
    times 510 - ($ - $$) db 0
    db 0x55, 0xaa
;-----------------------------------
;-----------------------------------
;-----------------------------------